extends PanelContainer

signal slot_clicked(index : int, button : int)

@onready var texture_rect : TextureRect = $MarginContainer/TextureRect
@onready var quantity_label : Label = $QuantityLabel
@onready var selection_rect = $MarginContainer/selectionRect
@onready var slot_background = $MarginContainer/slot_background


func set_slot_data(slot_data : SlotData) -> void:
	var item_data = slot_data.item_data
	texture_rect.texture = item_data.texture
	tooltip_text = "%s\n%s" % [item_data.name, item_data.description]
	
	if slot_data.quantity > 1:
		quantity_label.text = "x%s" % slot_data.quantity
		quantity_label.show()
	else:
		quantity_label.hide()


func _on_gui_input(event : InputEvent) -> void:
	if event is InputEventMouseButton \
			and (event.button_index == MOUSE_BUTTON_LEFT \
			or event.button_index == MOUSE_BUTTON_RIGHT) \
			and event.is_pressed():
		slot_clicked.emit(get_index(),event.button_index)
		#slot_background.visible = !slot_background.visible

func set_highlighted(is_highlighted : bool) -> void:
	if is_highlighted:
		#modulate = Color(.5,1,1,1)
		selection_rect.visible = true
	else:
		#modulate = Color(1,1,1,1)
		selection_rect.visible = false
